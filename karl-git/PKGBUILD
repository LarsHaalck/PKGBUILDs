# Maintainer: Lars Haalck
pkgname=karl-git
pkgver=r19.d15e061
pkgrel=1
pkgdesc='A clipboard manager'
arch=('i686' 'x86_64')
url='https://github.com/LarsHaalck/karl'
license=('MIT')
makedepends=('git' 'cargo')
provides=('karl')
conflicts=('karl')
source=("$pkgname::git+$url")
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target

  cd "$pkgname"
  cargo build --release --frozen
}

package() {
  cd "$pkgname"
  install -Dm755 target/release/karl -t "$pkgdir/usr/bin/"
  install -Dm755 scripts/rofi-karl -t "$pkgdir/usr/bin/"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim:set ts=2 sw=2 et:

