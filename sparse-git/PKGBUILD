# Maintainer: Lars Haalck
pkgname=sparse-git
pkgver=r54.5502c4d
pkgrel=1
pkgdesc='A tui matrix client '
arch=('i686' 'x86_64')
url='https://github.com/ftilde/sparse'
license=('MIT')
makedepends=('git' 'cargo')
provides=('sparse')
conflicts=('sparse')
source=("$pkgname::git+$url")
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target

  cd "$pkgname"
  cargo build --release --frozen
}

package() {
  cd "$pkgname"
  install -Dm755 target/release/sparse -t "$pkgdir/usr/bin/"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim:set ts=2 sw=2 et:

